;-DARK AI ANIMATIONS
:idle=0:animate=92
:movingforwards=1:aisetspeed=250,setanimatespeed=120
:runningforwards=1:aisetspeed=200,setanimatespeed=120

;-See target (enemy or generator)
:state=0,aicanshoot=1:aicleartarget,aisettarget
:state=0,aihastarget=0:state=1
:state=0,aihastarget=1:aimovetotarget
:state=0,aihastarget=1,aitargetdistwithin=160,random=4:aistop,setaiactive=0,etimerstart,state=2
;-Antistop bug
:state=0,idle=1,random=5:state=1
;-Run MAIN script for path finding
:state=1:aistop,setaiactive=0,runfpidefault=1

;-Non DarkAI the logic of convergence
:state=2,aicanshoot=1,aitargetdistfurther=40:movefore=2,airotatetotarget
:state=2,raycast=20 45:freeze,state=3
:state=2,aitargetdistwithin=50:freeze,state=300
:state=2,etimergreater=70:state=3
:state=3:setaiactive=1,airotatetotarget,aicleartarget,state=0

;-ATTACK GENERATOR & ENEMIES
;-Random attack animation
:state=300:setaiactive=1,aistop,aisetspeed=100,setanimatespeed=100,dimlocalvar=anim,state=301
:state=301,random=2:setvar=anim 98,sound=audiobank\zombie_apocalypse\malemoan1.wav,state=302
:state=301,random=2:setvar=anim 99,sound=audiobank\zombie_apocalypse\malemoan2.wav,state=302
:state=301,random=2:setvar=anim 100,sound=audiobank\zombie_apocalypse\malemoan2.wav,state=302
:state=301,random=2:setvar=anim 101,sound=audiobank\zombie_apocalypse\malemoan1.wav,state=302
:state=302:setframe=%anim,state=310

;-Rotate to target
:stategreater=300:airotatetotarget

;-Attack animation
:state=310:incframe=%anim
:state=310,framebeyond=%anim 50,aitargetdistwithin=70:aisetmeleedamage=25,aiusemelee=1
:state=310,framebeyond=%anim 50:state=311
:state=311:incframe=%anim
:state=311,frameatend=%anim:state=312

;-Target not close, cancel the attack
:stategreater=300,aitargetdistfurther=80:state=312

;- End of attack
:state=312:setframe=92,state=0

;-Remove when player is so far
:plrdistfurther=1000:aistop,airemove,destroy