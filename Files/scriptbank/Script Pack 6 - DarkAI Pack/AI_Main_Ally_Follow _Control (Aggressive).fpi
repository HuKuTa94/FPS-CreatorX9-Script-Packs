;Скрипт Искусственного Интелекта
;Автор: Никита (www.fpsc.forum24.ru)
;Дата: 04.01.2011
;Требования: необходим патч 1.17 и выше. 
;Версия скрипта: 2.0

;Описание: Управляемый союзник (агрессивное поведение!) стреляет в противника как только его увидит.
;Управление: F - следовать, повторное нажатие - прекращает следовать за игроком.
;            H - дает приказ найти ближайшее укрытие, повторное нажатие - отменяет приказ.
;            Так же можно отменить приказ нажав клавишу F. 


;===Анимации движения===
:idle=1,keypressed=46 0:animate=1                                            ; - Стоит
:movingforwards=1,keypressed=46 0:animate=5,animationnormal,aisetspeed=200   ; - Идет вперед стоя
:movingbackwards=1,keypressed=46 0:animate=2,animationreverse,aisetspeed=85  ; - Идет назад стоя
:runningforwards=1,keypressed=46 0:animate=5,animationnormal,aisetspeed=200  ; - Бежит вперед

:idle=1,keypressed=46 1:animate=31                                            ; - Сидит
:movingforwards=1,keypressed=46 1:animate=32,animationnormal,aisetspeed=60    ; - Идет вперед сидя
:movingbackwards=1,keypressed=46 1:animate=32,animationreverse,aisetspeed=60  ; - Идет назад сидя
:runningforwards=1,keypressed=46 1:animate=32,animationnormal,aisetspeed=60   ; - Идет вперед сидя



;===Начало скрипта===
:state=0:setaiactive=1,alwaysactive=1,aiatackwareness=1,reloadweapon,state=1

;===Смотрит на игрока, если он близко===
:state=1:aistop,freeze,setaicleartarget,aiattackawareness=1,aisettarget    ; - Стоит неподвижно
;:state=1,plrdistwithin=250:rotatetoplr                                    ; - Если игрок приблизился на 250, то смотрит на него

;===Клавиши управления===
:state=1,keypressed=35 1:state=5          ; - Нажата клавиша Н. Перс ищет укрытие
:state=5,keypressed=35 0:state=6
:state=6,keypressed=35 1:state=7,aistop   ; - Повторно нажата клавиша Н. Перс прекращает искать укрытие
:state=7,keypressed=35 0:state=1
:state=6,keypressed=33 1:state=8          ; - Нажата клавиша F во время исполнения приказа H
:state=8,keypressed=33 0:state=3,aistop
:state=3,keypressed=35 1:state=9          ; - Нажата клавиша Н во время исполнения приказа F
:state=9,keypressed=35 0:state=6,aistop
:state=1,keypressed=33 1:state=2,aicleartarget          ; - Нажата клавиша F. Перс cледует за игроком
:state=2,keypressed=33 0:state=3
:state=3,keypressed=33 1:state=4          ; - Повторно нажата клавиша F. Перс перестает следовать за игроком
:state=4,keypressed=33 0:state=1

;===Следует за игроком===
:state=3,plrcannotbeseen:aifollowplr=1,aisettarget    ; - Следует за игроком, если его не видит
:state=3,plrdistfurther=60:aifollowplr=1,aisettarget  ; - Cледует за игроком, если он отдалился
:state=3,plrdistwithin=60:aistop                      ; - Останавливается, если игрок слишком близко

;===Ищет укрытие==
:state=6,etimergreater=2000:state=3
:state=6:aimovetocover=1                  ; - Движется в ближайшее укрытие
:state=6,aiatcover=1:state=1              ; - Перс нашел укрытие


;///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
;//// Атака /////

;===Стреляет в противника===
:state=1,aiaction=1:aiusefullaim=1
:state=1,aiaction=2:aiusefullaim=0
:state=1,aihastarget=0:aiattackawareness=1,aisettarget,airotatetotarget
:state=1,aihastarget=1,aicanshoot=0:setaicleartarget,aiattackawareness=1,aisettarget,airotatetotarget
:state=1,aihastarget=1:setaicleartarget,aiattackawareness=1,aisettarget,airotatetotarget
:state=1,hasweapon=1,ifweapon=1,aicanshoot=1,random=5:useweapon,rundecal=6

:state=3,aiaction=1:aiusefullaim=1
:state=3,aiaction=2:aiusefullaim=0
:state=3,aihastarget=0:aiattackawareness=1,aisettarget,airotatetotarget
:state=3,aihastarget=1,aicanshoot=0:setaicleartarget,aiattackawareness=1,aisettarget,airotatetotarget
:state=3,aihastarget=1:setaicleartarget,aiattackawareness=1,aisettarget,airotatetotarget
:state=3,hasweapon=1,ifweapon=1,aicanshoot=1,aitargetdistfurther=100,random=1:useweapon,rundecal=6
:state=3,hasweapon=1,ifweapon=1,aicanshoot=1,aitargetdistwithin=100:useweapon,rundecal=6

:state=6,aiaction=1:aiusefullaim=1
:state=6,aiaction=2:aiusefullaim=0
:state=6,aihastarget=0:aiattackawareness=1,aisettarget,airotatetotarget
:state=6,aihastarget=1,aicanshoot=0:setaicleartarget,aiattackawareness=1,aisettarget,airotatetotarget
:state=6,aihastarget=1:setaicleartarget,aiattackawareness=1,aisettarget,airotatetotarget
:state=6,hasweapon=1,ifweapon=1,aicanshoot=1:useweapon,rundecal=6

;===Перезарядка (стоя)===
:state=1,ifweapon=0,keypressed=46 0:state=10,setframe=6,aistop,setaiactive=0
:state=3,ifweapon=0,keypressed=46 0:state=11,setframe=6,aistop,setaiactive=0
:state=6,ifweapon=0,keypressed=46 0:state=12,setframe=6,aistop,setaiactive=0
:state=10:incframe=6,aisetspeed=90
:state=10,frameatend=6:reloadweapon,state=1,setaiactive=1
:state=11:incframe=6,aisetspeed=90
:state=11,frameatend=6:reloadweapon,state=3,setaiactive=1
:state=12:incframe=6,aisetspeed=90
:state=12,frameatend=6:reloadweapon,state=6,setaiactive=1

;===Перезарядка (сидя)===
:state=1,ifweapon=0,keypressed=46 1:state=13,setframe=36,aistop,setaiactive=0
:state=3,ifweapon=0,keypressed=46 1:state=14,setframe=36,aistop,setaiactive=0
:state=6,ifweapon=0,keypressed=46 1:state=15,setframe=36,aistop,setaiactive=0
:state=13:incframe=36,aisetspeed=90
:state=13,frameatend=36:reloadweapon,state=1,setaiactive=1
:state=14:incframe=36,aisetspeed=90
:state=14,frameatend=36:reloadweapon,state=3,setaiactive=1
:state=15:incframe=36,aisetspeed=90
:state=15,frameatend=36:reloadweapon,state=6,setaiactive=1

 
;Конец Скрипта
