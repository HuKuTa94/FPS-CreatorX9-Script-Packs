;Скрипт Искусственного Интелекта
;Автор: Никита (www.fpsc.forum24.ru)
;Дата: 05.01.2011
;Требования: необходим патч 1.17 и выше. 
;Версия скрипта: 2.0
;Описание: скрипт для зомби "Slow Zombie (мужчина)" (медленный!) из пака "Zombie Apocalypse" 


;===Анимации движения===
:idle=1:animate=1                                            ; - Стоит
:movingforwards=1,:animate=92,animationnormal,aisetspeed=60  ; - Идет вперед
:movingbackwards=1:animate=92,animationreverse,aisetspeed=60 ; - Идет назад
:runningforwards=1:animate=92,animationnormal,aisetspeed=60  ; - Идет вперед

;===Начало скрипта===
:state=0:setaiactive=1,alwaysactive=1,aiatackwareness=1,state=1

;===Определение цели===
:state=1,aihastarget=0:aisettarget
:state=1,aihastarget=1:state=2
:state=1,aiheardsound=1800:aimovetosound

:state=2,aitargetdistfurther=65:airotatetotarget,setaiactive=1,aisetspeed=230,aimovetotarget
:state=2,aitargetdistwithin=65:aistop,airotatetotarget,aisetspeed=115,setaiactive=0,state=10
:state=2,aihastarget=0:state=1,aicleartarget


;////////////////////////////////////////////////////////////////////////////////////////////
;//// Атака ////
;===Выбор атаки===
:state=10,random=5:state=11,setframe=93,setaiactive=0,sound=audiobank\zombie_apocalypse\zombie1intro.wav
:state=10,random=5:state=12,setframe=94,setaiactive=0,sound=audiobank\zombie_apocalypse\zombie1growl.wav

;===Атака 1===
:state=11:incframe=93,airotatetoplr,aisettarget,aisetspeed=100
:state=11,framebeyond=93 44,aitargetdistwithin=60:aisetmeleedamage=50,aiusemelee=1,aicleartarget,aiattackawareness=1,sound=audiobank\misc\melee.wav,state=111
:state=11,framebeyond=93 44,aitargetdistfurther=70:state=20
:state=111:incframe=93,airotatetotarget,aisettarget,aisetspeed=100
:state=111,frameatend=93:state=20

;===Атака 2===
:state=12:incframe=94,airotatetoplr,aisettarget,aisetspeed=100
:state=12,framebeyond=94 55,aitargetdistwithin=60:aisetmeleedamage=50,aiusemelee=1,aicleartarget,aiattackawareness=1,sound=audiobank\misc\melee.wav,state=121
:state=12,framebeyond=94 55,aitargetdistfurther=60:state=20
:state=121:incframe=94,airotatetotarget,aisettarget,aisetspeed=100
:state=121,frameatend=94:state=20

:state=20:setframe=1,setaiactive=1,state=1


;Конец Скрипта


