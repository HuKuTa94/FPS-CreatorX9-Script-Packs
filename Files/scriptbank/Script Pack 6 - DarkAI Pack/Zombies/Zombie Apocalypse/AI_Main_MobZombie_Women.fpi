;Скрипт Искусственного Интелекта
;Автор: Никита (www.fpsc.forum24.ru)
;Дата: 05.01.2011
;Требования: необходим патч 1.17 и выше. 
;Версия скрипта: 2.0
;Описание: скрипт для зомби "Mob Zombie (женщина)" (быстрый!) из пака "Zombie Apocalypse" 


;===Анимации движения===
:idle=1:animate=94                                            ; - Стоит
:movingforwards=1,:animate=92,animationnormal,aisetspeed=240  ; - Бежит вперед
:movingbackwards=1:animate=92,animationreverse,aisetspeed=240 ; - Бежит назад
:runningforwards=1:animate=92,animationnormal,aisetspeed=240  ; - Бежит вперед

;===Начало скрипта===
:state=0:setaiactive=1,alwaysactive=1,aiatackwareness=1,state=1

;==Поиск цели==
:state=1,aihastarget=0:aisettarget
:state=1,aihastarget=1:state=2
:state=1,aiheardsound=1800:aimovetosound

:state=2,aitargetdistfurther=65:airotatetotarget,setaiactive=1,aisetspeed=230,aimovetotarget
:state=2,aitargetdistwithin=65:aistop,airotatetotarget,aisetspeed=115,setaiactive=0,state=10
:state=2,aihastarget=0:state=1
:state=2,aihastarget=0:state=1,aicleartarget

;////////////////////////////////////////////////////////////////////////////////////////////
;//// Атака ////
;===Выбор атаки===
:state=10,random=5:state=11,setframe=98,setaiactive=0,sound=audiobank\zombie_apocalypse\femalescream1.wav
:state=10,random=5:state=12,setframe=99,setaiactive=0,sound=audiobank\zombie_apocalypse\femalescream2.wave

;===Атака 1===
:state=11:incframe=98,airotatetotarget,aisettarget,aisetspeed=180
:state=11,framebeyond=98 40,aitargetdistwithin=65:aisetmeleedamage=30,aiusemelee=1,aicleartarget,aiattackawareness=1,sound=audiobank\misc\melee.wav,state=111
:state=11,framebeyond=98 40,aitargetdistfurther=65:state=20
:state=111:incframe=98,airotatetotarget,aisettarget,aisetspeed=180
:state=111,framebeyond=98 60,aitargetdistwithin=65:aisetmeleedamage=30,aiusemelee=1,aicleartarget,aiattackawareness=1,sound=audiobank\misc\melee.wav,state=112
:state=111,framebeyond=98 60,aitargetdistfurther=65:state=20
:state=112:incframe=98,airotatetotarget,aisettarget
:state=112,frameatend=98:state=20

;===Атака 2===
:state=12:incframe=99,airotatetoplr,aisettarget,aisetspeed=180
:state=12,framebeyond=99 50,aitargetdistwithin=65:aisetmeleedamage=30,aiusemelee=1,aicleartarget,aiattackawareness=1,sound=audiobank\misc\melee.wav,state=121
:state=12,framebeyond=99 50,aitargetdistfurther=65:state=20
:state=121:incframe=99,airotatetotarget,aisettarget,aisetspeed=180
:state=121,frameatend=99:state=20

:state=20:setframe=94,setaiactive=1,state=1


;Конец Скрипта


