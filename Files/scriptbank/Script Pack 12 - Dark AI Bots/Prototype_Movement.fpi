;—тарт скрипта
:state=0:addaiteam=1,aiaddenemy=1 2,setaiactive=1,aiattackwareness=0,aiautofactionoff=1,reloadweapon,alwaysactive=1,state=1

;ѕередвижение бота
;-ƒвижение задом, когда игрок р€дом (игрок смотрит на бота)
:state=1,aicanshoot=0:rotatetoplr,rotatey=180
:state=1,idle=1:animate=1,aifollowplr=0,airotatetotarget
:state=1,movingforwards=1:animate=2,animationreverse,setanimatespeed=150,aisetspeed=100
:state=1,movingbackwards=1,aicanshoot=0:animate=2,animationnormal,setanimatespeed=100,aisetspeed=90
:state=1,movingbackwards=1,aicanshoot=1:animate=2,animationreverse,setanimatespeed=150,aisetspeed=100
:state=1,plrdistfurther=65:aifollowplr=1
;--(игрок не смотрит на бота)
:state=1,inview=0:state=2
:state=2,inview=1:state=1
:state=2:rotatetoplr,rotatey=180
:state=2,plrdistfurther=65:aifollowplr=1
;-ƒвижение передом, следует медленно за игроком (игрок смотрит на бота)
:state=1,plrdistfurther=90:animate=2,state=3
:state=3,plrdistwithin=70:state=1
:state=3,movingforwards=1:animationnormal,setanimatespeed=140,aisetspeed=150,animate=2
:state=3,movingbackwards=1:animationreverse,setanimatespeed=170,aisetspeed=150,animate=2
:state=3,plrdistfurther=70:aifollowplr=1
;--(игрок не смотрит на бота)
:state=3,inview=0:state=4
:state=4,inview=1:state=3
:state=4,plrdistfurther=70:aifollowplr=1
;-ƒвижение передом, следует быстро за игроком (игрок смотрит на бота)
:state=3,plrdistfurther=150:state=5
:state=5,movingforwards=1,aicanshoot=0:animationnormal,setanimatespeed=100,aisetspeed=300
:state=5,movingforwards=1,aicanshoot=1:animationreverse,setanimatespeed=80,aisetspeed=300
:state=5,runningforwards=1,aicanshoot=0:animationnormal,setanimatespeed=100,aisetspeed=250
:state=5,runningforwards=1,aicanshoot=1:animationreverse,setanimatespeed=80,aisetspeed=250
:state=5:aifollowplr=1,animate=5
:state=5,plrdistwithin=85:animate=2,aisetspeed=150,state=3
;--(игрок не смотрит на бота)
:state=5,inview=0:aisetspeed=220,state=6
:state=6,inview=1:state=5
:state=6:aifollowplr=1
:state=6,plrdistwithin=85:animate=2,aisetspeed=150,state=4
;-ƒогон€ет игрока, когда нет патронов в магазине (общий дл€ обеих случаев игрок видит\не видит бота)
:state=7:aifollowplr=1,animate=5,animationnormal
:state=7,movingforwards=1:setanimatespeed=100,aisetspeed=300
:state=7,runningforwards=1:setanimatespeed=100,aisetspeed=250
:state=7,plrdistwithin=85:aisetspeed=90,setaiactive=0,setframe=6,activate=12,state=8
